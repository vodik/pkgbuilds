# Maintainer: Simon Gomizelj <simongmzlj@gmail.com>

pkgname=envoy-git
pkgver=1.0.37.g804bdf7
pkgrel=1
pkgdesc="A ssh-agent/gpg-agent keychain and process monitor"
arch=('i686' 'x86_64')
url="http://github.com/vodik/envoy"
license=('GPL')
depends=('openssh' 'systemd')
optdepends=('gnupg: gpg-agent support')
makedepends=('git')
conflicts=('envoy')
provides=('envoy')
source=('git://github.com/vodik/envoy.git')
sha1sums=('SKIP')

pkgver() {
  cd "envoy"
  git describe | sed 's/^v//;s/-/./g'
}

build() {
  make -C "envoy"
}

package() {
  make -C "envoy" DESTDIR="$pkgdir" install
}

# vim: ft=sh syn=sh et
