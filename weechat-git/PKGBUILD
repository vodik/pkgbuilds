# Maintainer: Dmitry Korzhevin <dkorzhevin AT gmail DOT com>
# Contributor: Mark Foxwell <fastfret79@archlinux.org.uk>
# Contributor: Licia Todd <tigrmesh at aol dot com>
# Contributor: Tim Zebulla <amon at faumrahrer dot de>
# Contributor: Simon Gomizelj <simongmzlj at gmail com>

pkgname=weechat-git
pkgver=0.4.0.97.gafc1dd4
pkgrel=1
pkgdesc="Fast, light & extensible IRC client (curses UI)"
arch=('i686' 'x86_64')
url="http://www.weechat.org/"
license=('GPL3')
depends=('gnutls')
makedepends=('git' 'cmake' 'pkgconfig' 'perl' 'python2' 'lua' 'tcl' 'ruby' 'aspell' 'curl')
optdepends=('perl' 'python2' 'lua' 'tcl' 'ruby' 'aspell')
provides=('weechat')
conflicts=('weechat')
options=(!libtool)
source=('git://git.sv.gnu.org/weechat.git')
sha1sums=('SKIP')

pkgver() {
  cd "weechat"
  git describe | sed 's/^v//;s/-/./g'
}

build() {
  cd "weechat"
  cmake -DPREFIX=/usr \
        -DPYTHON_EXECUTABLE=/usr/bin/python2 \
        -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so
}

package() {
  make -C "weechat" DESTDIR="${pkgdir}" install
}
