# $Id: PKGBUILD 146745 2012-01-16 22:52:52Z dreisner $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Fixes from @nullren

pkgname=libassuan-git
pkgver=2.1.1.1.gfd255da
pkgver() {
  cd "libassuan"
  git describe | sed 's/^libassuan-//;s/-/./g'
}
pkgrel=1
pkgdesc="An IPC library used by some GnuPG related software (git version)"

arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnupg.org/related_software/libassuan"
depends=('libgpg-error')
makedepends=('git')
options=('!libtool')
provides=('libassuan')
conflicts=('libassuan')
install=install
source=('git+git://git.gnupg.org/libassuan.git')
sha1sums=('SKIP')

build() {
  cd "libassuan"

   # patch things and don't build doc
   sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/g' configure.ac
   sed -i '/doc.Makefile/d' configure.ac
   sed -i 's/src doc tests/src tests/' Makefile.am
   autoreconf -fiv

   ./configure \
     --prefix=/usr \
     --enable-maintainer-mode

   make
}

check() {
  make -C "libassuan" check
}

package() {
  make -C "libassuan" DESTDIR="$pkgdir" install
}
