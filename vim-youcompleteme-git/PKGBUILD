# Maintainer: Daniel Micay <danielmicay@gmail.com>

pkgname=vim-youcompleteme-git
pkgver=589.b9e4712
pkgver() {
  cd "YouCompleteMe"
  echo $(git rev-list --count makepkg).$(git rev-parse --short makepkg)
}
pkgrel=1
pkgdesc='A code-completion engine for Vim'

arch=(i686 x86_64)
url='http://valloric.github.com/YouCompleteMe/'
license=('GPL3')
groups=('vim-plugins')
depends=('vim' 'clang' 'python')
makedepends=('git' 'cmake')
provides=('vim-youcompleteme')
conflicts=('vim-youcompleteme')
source=('git+git://github.com/vodik/YouCompleteMe.git#branch=python3'
        'git+git://github.com/davidhalter/jedi.git')
sha1sums=('SKIP' 'SKIP')
install=vimdoc.install

build() {
  cd "YouCompleteMe/cpp"
  cmake -G "Unix Makefiles" -DUSE_SYSTEM_LIBCLANG=ON \
    -DPYTHON_INCLUDE_DIR=/usr/include/python3.3m \
    -DPYTHON_LIBRARY=/usr/lib/libpython3.3m.so
  make ycm_core
}

package() {
  mkdir -p "$pkgdir/usr/share/vim/vimfiles"

  cd "$srcdir/YouCompleteMe"
  cp -a autoload doc plugin python "$pkgdir/usr/share/vim/vimfiles"
  ln -sf /usr/lib/llvm/libclang.so "$pkgdir/usr/share/vim/vimfiles/python/"

  cd "$srcdir/jedi"
  cp -a jedi "$pkgdir/usr/share/vim/vimfiles/python/completers/python/jedi/"
}
