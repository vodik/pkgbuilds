# Maintainer: Daniel Micay <danielmicay@gmail.com>

pkgname=kmscon-git
pkgver=7.42.g4e0b981
pkgver() {
  cd "kmscon"
  git describe | sed 's/^kmscon-//;s/-/./g'
}
pkgrel=1
pkgdesc='Terminal emulator based on Kernel Mode Setting (KMS)'

arch=('i686' 'x86_64')
url='https://github.com/dvdhrm/kmscon'
license=('MIT')
depends=('systemd' 'libdrm' 'mesa' 'libegl' 'libgbm' 'libgles' 'pango' 'libxkbcommon' 'xkeyboard-config')
makedepends=('git' 'libxslt' 'docbook-xsl' 'linux-api-headers')
options=('!libtool')
provides=('kmscon')
conflicts=('kmscon')
source=('git+git://github.com/dvdhrm/kmscon.git')
sha1sums=('SKIP')

build() {
  cd "kmscon"
  ./autogen.sh --prefix=/usr --disable-wlterm
  make
}

package() {
  cd "kmscon"
  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname" "$pkgdir/usr/lib/systemd/system"
  cp COPYING "$pkgdir/usr/share/licenses/$pkgname/"
  cp docs/kmscon{,vt@}.service "$pkgdir/usr/lib/systemd/system/"
}
