# Maintainer: Daniel Micay <danielmicay@gmail.com>

pkgname=termite-git
pkgver=6.31.g0d0f541
pkgver() {
  cd "termite"
  git describe | sed 's/^v//;s/-/./g'
}
pkgrel=1
pkgdesc="A simple VTE-based terminal"

arch=('i686' 'x86_64')
url="https://github.com/thestinger/termite/"
license=('LGPL')
depends=('vte3-select-text>=0.34.1')
makedepends=('git')
provides=('termite')
conflicts=('termite')
backup=('etc/xdg/termite/config')
install=termite-git.install
source=('git+git://github.com/thestinger/termite.git')
sha1sums=('SKIP')

build() {
  cd "termite"

  # workaround for submodules
  git submodule init
  git config submodule.qux/util.url "$srcdir/termite-module-util"
  git submodule update

  make
}

package() {
  cd "termite"
  make PREFIX="/usr" DESTDIR="$pkgdir" install
  install -Dm644 config "$pkgdir/etc/xdg/termite/config"
}
