# Maintainer: Simon Gomizelj <simongmzlj@gmail.com>

pkgname=envoy-git
pkgver=3.7.g7725b48
pkgver() {
  cd "envoy"
  git describe | sed 's/^v//;s/-/./g'
}
pkgrel=1
pkgdesc="A ssh-agent/gpg-agent keychain and process monitor"

arch=('i686' 'x86_64')
url="http://github.com/vodik/envoy"
license=('GPL')
depends=('openssh' 'systemd')
optdepends=('gnupg: gpg-agent support')
makedepends=('git')
conflicts=('envoy')
provides=('envoy')
source=('git+git://github.com/vodik/envoy.git'
        'git+git://github.com/vodik/clique.git')
sha1sums=('SKIP' 'SKIP')

build() {
  cd "envoy"

  # workaround for submodules
  git submodule init
  git config submodule.qux/clique.url "$srcdir/clique"
  git submodule update

  make
}

package() {
  make -C "envoy" DESTDIR="$pkgdir" install
}

# vim: ft=sh syn=sh et
