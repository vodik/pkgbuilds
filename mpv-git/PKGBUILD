# Maintainer: RudolfPolzer <divVerent@xonotic.org>
# Based on the mplayer2-lite-git PKGBUILD

pkgname=mpv-git
pkgver=35384.2396e62
pkgver() {
  cd "mpv"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}
pkgrel=1
pkgdesc="A movie player and encoder for linux (uses dynamically linked ffmpeg; git version)"

arch=('i686' 'x86_64')
license=('GPL')
url="https://github.com/mpv-player/mpv"
depends=('libcaca' 'portaudio' 'ffmpeg' 'lcms2' 'libdvdread' 'smbclient' 'libmng' 'libcdio-paranoia' 'libxinerama' 'lirc-utils' 'libxss' 'desktop-file-utils' 'libquvi' 'mpg123' 'libxv' 'libxkbcommon')
makedepends=('git' 'mesa' 'python-docutils')
provides=('mpv')
conflicts=('mpv')
install=mpv.install
source=('git+http://github.com/mpv-player/mpv#branch=master'
        'mpv.desktop')
md5sums=('SKIP'
        'afa04e3bdb3ef16b78804760c63a3d9a')

build() {
  # Custom CFLAGS break the mpv build
  unset CFLAGS
  unset LDFLAGS
  cd "mpv"

  ./configure --prefix=/usr \
      --extra-cflags="-I/usr/include/samba-4.0" \
      --disable-lircc \
      --disable-joystick \
      --disable-radio \
      --disable-radio-capture \
      --disable-radio-v4l2 \
      --disable-rpath \
      --disable-vstream \
      --disable-libbs2b \
      --disable-sdl2

  make
}

package() {
  make -C mpv DESTDIR="$pkgdir" install

  install -d "$pkgdir/usr/share/doc/mpv/examples"

  # some extra useful stuff "make install" doesn't install
  install -m644 "$srcdir/mpv/etc/"{input,example,encoding-example-profiles}.conf "$pkgdir/usr/share/doc/mpv/examples"
  install -m644 "$srcdir/mpv/DOCS/"{encoding.rst,tech-overview.txt} "$pkgdir/usr/share/doc/mpv"
  install -m755 "$srcdir/mpv/TOOLS/mpv_identify.sh" "$pkgdir/usr/bin"
  install -Dm644 "$srcdir/mpv/etc/mplayer.xpm" "$pkgdir/usr/share/pixmaps/mpv.xpm"

  # file association
  install -Dm644 "$srcdir/mpv.desktop" "$pkgdir/usr/share/applications/mpv.desktop"
}
