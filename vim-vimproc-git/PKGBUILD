# Maintainer: anergy <anergy.25110321@gmail.com>

pkgname=vim-vimproc-git
pkgver=7.0.107.g602ffe5
pkgver() {
  cd "vimproc"
  git describe --tags | sed 's/^ver\.//;s/-/./g'
}
pkgrel=1
pkgdesc="interactive command execution in vim"

arch=(i686 x86_64)
url="https://github.com/Shougo/vimproc"
license=('MIT')
groups=('vim-plugins')
depends=('vim')
makedepends=('git')
provides=('vim-vimproc')
conflicts=('vim-vimproc')
source=('git+git://github.com/Shougo/vimproc.git')
sha1sums=('SKIP')
install=vimdoc.install

build() {
  make -C "vimproc" -f make_unix.mak
}

package() {
  cd "vimproc"
  mkdir -p "$pkgdir/usr/share/vim/vimfiles/autoload"
  cp -a autoload/vimproc* "$pkgdir/usr/share/vim/vimfiles/autoload"
  cp -a doc plugin "$pkgdir/usr/share/vim/vimfiles"
}

# vim:set ts=2 sw=2 et:
