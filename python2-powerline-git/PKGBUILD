# Maintainer: Kim Silkeb√¶kken <kim.silkebaekken+aur@gmail.com>

pkgname=python2-powerline-git
pkgver=20130313
pkgver() {
  date +%Y%m%d
}
pkgrel=1
pkgdesc='The ultimate statusline/prompt utility.'

url='https://github.com/Lokaltog/powerline'
license=('MIT')
arch=('any')
depends=('python2>=2.6')
makedepends=('git' 'python2-distribute')
optdepends=('python2-psutil: improved system information'
  'python2-pygit2: improved git support')
replaces=('powerline-git')
source=("git+git://github.com/Lokaltog/powerline.git#branch=develop")
sha1sums=('SKIP')
install=powerline.install

package() {
	cd "powerline"
  python2 setup.py install --root="$pkgdir" --optimize=1
	install -dm755 "$pkgdir/usr/share/fonts/OTF/"
	install -dm755 "$pkgdir/etc/fonts/conf.avail"
	install -dm755 "$pkgdir/etc/fonts/conf.d"
	install -m644 "font/PowerlineSymbols.otf" "$pkgdir/usr/share/fonts/OTF/PowerlineSymbols.otf"
	install -m644 "font/10-powerline-symbols.conf" "$pkgdir/etc/fonts/conf.avail/10-powerline-symbols.conf"
	ln -s "../conf.avail/10-powerline-symbols.conf" "$pkgdir/etc/fonts/conf.d/10-powerline-symbols.conf"

	install -dm755 "$pkgdir/usr/share/vim/vimfiles/plugin"
	# install -dm755 "$pkgdir/usr/share/zsh/site-contrib"
	# install -dm755 "$pkgdir/usr/share/zsh/site-contrib"
	# install -dm755 "$pkgdir/usr/share/tmux"
	install -m644 "powerline/bindings/vim/plugin/powerline.vim" "$pkgdir/usr/share/vim/vimfiles/plugin/powerline.vim"
	# install -m644 "powerline/bindings/zsh/powerline.zsh" "$pkgdir/usr/share/zsh/site-contrib/powerline.zsh"
	# install -m644 "powerline/bindings/tmux/powerline.conf" "$pkgdir/usr/share/tmux/powerline.conf"
	install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
	install -m644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
