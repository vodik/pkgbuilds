# Maintainer: Daniel Micay <danielmicay@gmail.com>

pkgname=termite-git
pkgver=6.3.ga3b33d4
pkgrel=1
pkgdesc="A simple VTE-based terminal"
arch=('i686' 'x86_64')
url="https://github.com/thestinger/termite/"
license=('GPL')
depends=('vte3-select-text>=0.34.1')
makedepends=('git')
provides=('termite')
conflicts=('termite')
backup=('etc/xdg/termite/config')
install='termite-git.install'
source=('git://github.com/thestinger/termite.git')
sha1sums=('SKIP')

pkgver() {
  cd "termite"
  git describe | sed 's/^v//;s/-/./g'
}

build() {
  make -C "termite"
}

package() {
  cd "termite"
  make PREFIX="/usr" DESTDIR="$pkgdir" install
  install -Dm644 config "$pkgdir/etc/xdg/termite/config"
}
